block(`flappy) {
  alias(`ui:dom, `dom),
  
  clauses(`{
  
    // state
  
    game_running(session) -> 
      string(session).
    game_over(session) ->
      string(session).
    game_not_started(session) <-
      dom:session(session),
      !game_running(session),
      !game_over(session).
    
    player_x[session] = x ->
      string(session), float(x).

    player_y[session] = y -> 
      string(session), float(y).
      
    player_dy[session] = dy ->
      string(session), float(dy).
      
    // TODO randomize seed per session and per game  
      
    seed[] = seed ->
      int(seed).
      
    seed[] = 3.
    
    best_score[] = best_score ->
      int(best_score).
      
    // event handlers
    
    +game_running(session),
    -game_over(session),
    ^player_x[session] = 0f,
    ^player_y[session] = 45f,
    ^player_dy[session] = 0f <-
      !game_running@prev(session),
      +dom:click(session, _).
    
    -game_running(session),
    +game_over(session) <-
      +dom:frame(session),
      game_running@prev(session),
      collision(session).
      
    // TODO cant correct for frame length because datetime resolution is 1s
      
    ^player_x[session] = player_x@prev[session] + 3f,
    ^player_y[session] = player_y@prev[session] + player_dy@prev[session] <-
      +dom:frame(session),
      game_running@prev(session).
    
    ^player_dy[session] = player_dy@prev[session] + 0.3f <-
      +dom:frame(session),
      game_running@prev(session),
      !+dom:click(session, _).
     
    ^player_dy[session] = player_dy@prev[session] - 2f <-
      +dom:frame(session),
      game_running@prev(session),
      +dom:click(session, _).
      
    +best_score[] = 0 <-
      +game_running(_), // TODO make an init event?
      !(best_score@prev[] = _).
  
    ^best_score[] = int:max[best_score@prev[], score@prev[session]] <-
      +game_running(session). 
      
    // computed
    
    viewport_width[] = 100f.
    viewport_height[] = 100f.
    floor_height[] = 90f.
    
    player_fixed_x[] = 20f.
    player_width[] = 10f.
    player_height[] = 10f * (41f / 57f).
    
    obstacle_width[] = 10f.
    obstacle_spacing[] = 50f.
    gap_size[] = 35f.
      
    obstacle_visible(session, i) <-
      left_x = player_x[session] - obstacle_width[],
      right_x = player_x[session] + viewport_width[] + obstacle_width[],
      left_i = float:int:convert[float:ceil[left_x / obstacle_spacing[]]],
      right_i = float:int:convert[float:floor[right_x / obstacle_spacing[]]],
      int:range(left_i, right_i, 1, i).
      
    obstacle_x[session, i] = x ->
      string(session), int(i), float(x).
      
    obstacle_x[session, i] = x <-
      obstacle_visible(session, i),
      world_x = int:float:convert[i] * obstacle_spacing[],
      x = world_x - player_x[session].
      
    obstacle_y[session, i] = y ->
      string(session), int(i), float(y).
      
    obstacle_y[session, ii] = y <-
      series<< y = rnd_uniform_real<5f, 35f, seed>[ii] >>
      seed[] = seed,
      obstacle_visible(session, i),
      int:range(1, i, 1, ii). // hack to get deterministic random gen (i -> y)
      
    collision(session) -> 
      string(session).
      
    collision(session) <-
      dom:session(session),
      !(0f <= player_y[session] <= floor_height[] - player_height[]) .
      
    collision(session) <- 
      obstacle_x[session, i] - player_width[] <= player_fixed_x[] <= obstacle_x[session, i] + obstacle_width[],
      !(obstacle_y[session, i] <= player_y[session] <= obstacle_y[session, i] + gap_size[] - player_height[]).
      
    score[session] = score ->
      string(session), int(score).  
    
    score[session] = float:int:convert[float:floor[player_x[session] / obstacle_spacing[]]] <-.
      
    // ui
    
    flappy(session) <-
      dom:url_fragment(session, "#flappy").
    
    dom:node(session, id),
    dom:parent[session, id] = "root",
    dom:position[session, id] = 0,
    dom:tag[session, id] = "svg:svg",
    dom:attribute[session, id, "width"] = "480",
    dom:attribute[session, id, "viewBox"] = "10 0 80 100",
    dom:listen_to(session, id, "click") <-
      flappy(session),
      id = "viewport".
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 0,
    dom:tag[session, id] = "svg:rect",
    dom:attribute[session, id, "x"] = "0",
    dom:attribute[session, id, "y"] = "0",
    dom:attribute[session, id, "width"] = "100",
    dom:attribute[session, id, "height"] = "53",
    dom:attribute[session, id, "fill"] = "rgb(112, 197, 206)" <-
      flappy(session),
      id = "sky".
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 1,
    dom:tag[session, id] = "svg:image",
    dom:attribute[session, id, "x"] = "0",
    dom:attribute[session, id, "y"] = "52",
    dom:attribute[session, id, "width"] = "100",
    dom:attribute[session, id, "height"] = "43",
    dom:attribute[session, id, "preserveAspectRatio"] = "xMinYMin slice",
    dom:attribute[session, id, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/background.png" <-
      flappy(session),
      id = "background".
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 2,
    dom:tag[session, id] = "svg:rect",
    dom:attribute[session, id, "x"] = "0",
    dom:attribute[session, id, "y"] = "95",
    dom:attribute[session, id, "width"] = "100",
    dom:attribute[session, id, "height"] = "5" <- 
      flappy(session),
      id = "ground".
      
    dom:attribute[session, id, "fill"] = "rgb(222, 216, 149)" <-
      flappy(session),
      id = "ground",
      !collision(session).
      
    dom:attribute[session, id, "fill"] = "rgb(255, 0, 0)" <-
      flappy(session),
      id = "ground",
      collision(session).
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 3,
    dom:tag[session, id] = "svg:svg",
    dom:attribute[session, id, "x"] = "0",
    dom:attribute[session, id, "y"] = "0",
    dom:attribute[session, id, "width"] = "100",
    dom:attribute[session, id, "height"] = "100" <-
      flappy(session),
      id = "obstacles".
      
    dom:node(session, id_top_body),
    dom:parent[session, id_top_body] = "obstacles",
    dom:position[session, id_top_body] = (4*i) + 0, 
    dom:tag[session, id_top_body] = "svg:image",
    dom:attribute[session, id_top_body, "x"] = float:string:convert[x],
    dom:attribute[session, id_top_body, "y"] = "0",
    dom:attribute[session, id_top_body, "width"] = float:string:convert[obstacle_width[]],
    dom:attribute[session, id_top_body, "height"] = float:string:convert[y],
    dom:attribute[session, id_top_body, "preserveAspectRatio"] = "none",
    dom:attribute[session, id_top_body, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/pillar-bkg.png",
    
    dom:node(session, id_bottom_body),
    dom:parent[session, id_bottom_body] = "obstacles",
    dom:position[session, id_bottom_body] = (4*i) + 1, 
    dom:tag[session, id_bottom_body] = "svg:image",
    dom:attribute[session, id_bottom_body, "x"] = float:string:convert[x],
    dom:attribute[session, id_bottom_body, "y"] = float:string:convert[y+gap_size[]],
    dom:attribute[session, id_bottom_body, "width"] = float:string:convert[obstacle_width[]],
    dom:attribute[session, id_bottom_body, "height"] = float:string:convert[floor_height[]-y-gap_size[]],
    dom:attribute[session, id_bottom_body, "preserveAspectRatio"] = "none",
    dom:attribute[session, id_bottom_body, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/pillar-bkg.png",
    
    dom:node(session, id_top_head),
    dom:parent[session, id_top_head] = "obstacles",
    dom:position[session, id_top_head] = (4*i) + 2, 
    dom:tag[session, id_top_head] = "svg:image",
    dom:attribute[session, id_top_head, "x"] = float:string:convert[x-1f],
    dom:attribute[session, id_top_head, "y"] = float:string:convert[y-5f],
    dom:attribute[session, id_top_head, "width"] = float:string:convert[obstacle_width[]+2f],
    dom:attribute[session, id_top_head, "height"] = "5",
    dom:attribute[session, id_top_head, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/lower-pillar-head.png",
    
    dom:node(session, id_bottom_head),
    dom:parent[session, id_bottom_head] = "obstacles",
    dom:position[session, id_bottom_head] = (4*i) + 3, 
    dom:tag[session, id_bottom_head] = "svg:image",
    dom:attribute[session, id_bottom_head, "x"] = float:string:convert[x-1f],
    dom:attribute[session, id_bottom_head, "y"] = float:string:convert[y+gap_size[]],
    dom:attribute[session, id_bottom_head, "width"] = float:string:convert[obstacle_width[]+2f],
    dom:attribute[session, id_bottom_head, "height"] = "5",
    dom:attribute[session, id_bottom_head, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/lower-pillar-head.png" <-
  
      flappy(session),
      obstacle_x[session, i] = x,
      obstacle_y[session, i] = y,
      id_top_body = "obstacle-top-body-" + int:string:convert[i],
      id_bottom_body = "obstacle-bottom-body-" + int:string:convert[i],
      id_top_head = "obstacle-top-head-" + int:string:convert[i],
      id_bottom_head = "obstacle-bottom-head-" + int:string:convert[i].
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 4,
    dom:tag[session, id] = "svg:text",
    dom:attribute[session, id, "x"] = "50",
    dom:attribute[session, id, "y"] = "45",
    dom:attribute[session, id, "text-anchor"] = "middle",
    dom:attribute[session, id, "font-size"] = "6",
    dom:text[session, id] = "Click the screen to begin!" <-
      flappy(session),
      id = "start",
      game_not_started(session).
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 4,
    dom:tag[session, id] = "svg:svg",
    
    dom:node(session, id1),
    dom:parent[session, id1] = id,
    dom:position[session, id1] = 0,
    dom:tag[session, id1] = "svg:text",
    dom:attribute[session, id1, "x"] = "50",
    dom:attribute[session, id1, "y"] = "30",
    dom:attribute[session, id1, "text-anchor"] = "middle",
    dom:attribute[session, id1, "font-size"] = "6",
    dom:text[session, id1] = "Game Over :(",
    
    dom:node(session, id2),
    dom:parent[session, id2] = id,
    dom:position[session, id2] = 1,
    dom:tag[session, id2] = "svg:text",
    dom:attribute[session, id2, "x"] = "50",
    dom:attribute[session, id2, "y"] = "55",
    dom:attribute[session, id2, "text-anchor"] = "middle",
    dom:attribute[session, id2, "font-size"] = "6",
    dom:text[session, id2] = "Score " + int:string:convert[score[session]],
    
    dom:node(session, id3),
    dom:parent[session, id3] = id,
    dom:position[session, id3] = 2,
    dom:tag[session, id3] = "svg:text",
    dom:attribute[session, id3, "x"] = "50",
    dom:attribute[session, id3, "y"] = "65",
    dom:attribute[session, id3, "text-anchor"] = "middle",
    dom:attribute[session, id3, "font-size"] = "6",
    dom:text[session, id3] = "Best " + int:string:convert[best_score[]],
    
    dom:node(session, id4),
    dom:parent[session, id4] = id,
    dom:position[session, id4] = 3,
    dom:tag[session, id4] = "svg:text",
    dom:attribute[session, id4, "x"] = "50",
    dom:attribute[session, id4, "y"] = "85",
    dom:attribute[session, id4, "text-anchor"] = "middle",
    dom:attribute[session, id4, "font-size"] = "4",
    dom:text[session, id4] = "Click to play again!" <-
    
      flappy(session),
      id = "game-over",
      id1 = "game-over-1",
      id2 = "game-over-2",
      id3 = "game-over-3",
      id4 = "game-over-4",
      game_over(session).
      
    dom:node(session, id),
    dom:parent[session, id] = "viewport",
    dom:position[session, id] = 5,
    dom:tag[session, id] = "svg:image",
    dom:attribute[session, id, "x"] = float:string:convert[player_fixed_x[]],
    dom:attribute[session, id, "y"] = float:string:convert[player_y[session]],
    dom:attribute[session, id, "width"] = float:string:convert[player_width[]],
    dom:attribute[session, id, "height"] = float:string:convert[player_height[]],
    dom:attribute[session, id, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/flappy-base.png" <-
      flappy(session),
      id = "player".
      
  })    

} <-- .
